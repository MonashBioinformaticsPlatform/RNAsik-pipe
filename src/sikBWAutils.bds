
string mkBWAidx(SikBWAidxConfig sikBWAidxConf, string fasta, string bwaIdx, string[] idxDeps, string logFn) {

    string{} resources = sikBWAidxConf.idxRes
    string x = resources{"bwaExe"}
    int xCpu = resources{"idxCpu"}.parseInt()
    int xMem = resources{"idxMem"}.parseInt()

    string bwaCmdOpts = sikBWAidxConf.idxCmdOpts.join(" ")

    log("MSG: Beginng BWA indexing")
    task(idxDeps <- fasta, cpus := xCpu, mem := xMem, taskName := "Making bwa index") {
        sys $x index -p $bwaIdx \
                     $fasta \
                     $bwaCmdOpts > $logFn 2>&1
    }

    return bwaIdx
}

Sample mapBWA(SikBWAalignerConfig sikBWAalignerConf, Sample sample, string genomeIdx, string[] idxDeps) {

    Metadata metadata = sample.metadata
    Bams bams = sample.bams

    string{} resources = sikBWAalignerConf.alignerRes
    string x = resources{"bwaExe"}
    int xCpu = resources{"alignerCpu"}.parseInt()
    int xMem = resources{"alignerMem"}.parseInt()

    string bwaCmdOpts = sikBWAalignerConf.alignerCmdOpts.join(" ")

    string sampleName = metadata.sampleName
    string[] fq1 = metadata.fq1
    string[] fq2 = metadata.fq2

    string bamOut = bams.bamAlignerOut
    string bamLog = bams.bamAlignerLog

    int chk1 = fq1.size()
    int chk2 = fq2.size()

    assert("SikErr: Number of R1 and R2 reads is different -> " + sampleName , chk1 == chk2)

    string fq1Sub
    string fq2Sub

    if(chk1 == 1) {
        fq1Sub = fq1[0]
        fq2Sub = fq2[0]
    } else {
        fq1Sub = "<(zcat " + fq1.join(" ") + ")"
        fq2Sub = "<(zcat " + fq2.join(" ") + ")"

    }

    string[] deps
    deps += fq1
    deps += fq2
    deps += idxDeps

    string logMsg = "BWA mem aligning " + sampleName
    dep([bamOut, bamLog] <- deps, cpus := xCpu, mem := xMem, taskName := logMsg) {
        sys $x mem -t $xCpu \
                   $bwaCmdOpts \
                   $genomeIdx \
                   $fq1Sub $fq2Sub 2> $bamLog | samtools view -S -b - > $bamOut
    }

    bams.bamOut = bamOut
    return sample
}
