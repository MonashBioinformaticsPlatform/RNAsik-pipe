/*
Class to represent a sample input and output
*/

class MetadataSample {

    string sampleName
    string sampleGroup
    string[] fq1
    string[] fq2
    bool paired

    void MetadataSample(string line) {

        assert(!(line.startsWith("#")) | !(line.isEmpty()))

        //TODO check if the sample is paired end and set paired flag accordingly
        //Potentially can process paired and single end data together in the future
        //this.paired = paired

        string _path
        string[] samplesList = line.split("\t")

        int nCoreFields = 3

        int tot = samplesList.size()

        if(tot < nCoreFields+1) {
            error("There needs to be at least five columns in your samples sheet: SampleName, SampleGroup, Path, R1 and R2")
        }

        this.sampleName = samplesList[0]
        this.sampleGroup = samplesList[1]
        _path = samplesList[2]

        int nItems = tot - nCoreFields
        this.paired = (nItems % 2 == 0)

        if(paired) {
            for(int i : range(1, nItems)) {
                bool chk = (i % 2 != 0)
                int idx = i+nCoreFields-1

                if(chk) {
                    this.fq1.add(_path + "/" + samplesList[idx])
                } else {
                    this.fq2.add(_path + "/" + samplesList[idx])
                }
            }
        } else {
            for(int i : range(1, nItems)) {
                bool chk = (i % 2 != 0)
                int idx = i+nCoreFields
                this.fq1.add(_path + "/" + samplesList[idx])
            }
        }
    }
}

class MetadataAligner extends MetadataSample {

    string bamOut
    string bamLog

    void MetadataAligner(string line, string sikDir) {

        log("MSG: Output directory $sikDir")

        MetadataSample(line)

        string alignDir = sikDir+"/"+"alignerFiles"
        log("MSG: Making directory for alignment files $alignDir")
        if(!alignDir.exists()) {
            alignDir.mkdir()
        }

        this.bamOut = alignDir + "/" + this.sampleName + ".bam"
        this.bamLog = alignDir + "/" + "." + this.sampleName + ".siklog"
    }
}

class Metadata extends MetadataAligner {

    void Metadata(string line, string sikDir, string aligner) {
        log("MSG: Aligner type $aligner")

        MetadataAligner(line, sikDir)

        switch(aligner) {
            case 'star':
                this.bamOut = this.bamOut.replace(".bam", "_Aligned.out.bam")
                this.bamLog = this.bamLog.replace(".siklog", "_Log.out.final")
                break
            case 'hisat2':
                this.bamOut = this.bamOut.replace(".bam", "_aligned.bam")
                this.bamLog = this.bamLog.replace(".siklog", "_aligned.log")
                break

            case 'bwaMem':
                this.bamOut = this.bamOut.replace(".bam", "_bwaMem.bam")
                this.bamLog = this.bamLog.replace(".siklog", "_bwaMem.log")
                break
            default:
                error("SikErr: There needs to be an error here to make sure nothing else is passed through, other than allowed aligners")
        }
    }
}

class Samples {

    Metadata[] samples

    void Samples(string metadataFn, string sikDir, string aligner) {

        assert(metadataFn.exists())
        string[] metadata = metadataFn.readLines()

        for(string line : metadata) {

            if(line.startsWith("#") || line.isEmpty())  {
                continue
            }

            Metadata sample = new Metadata(line, sikDir, aligner)
            this.samples.add(sample)
        }
    }
}

