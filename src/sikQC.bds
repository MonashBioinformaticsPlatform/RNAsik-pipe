include "sikHeader.bds"
#---------------------------------------
# Functions to get different QC reports
#---------------------------------------

string[] getFastqcReport(string{} cmdExe, string sikDir, string[] fqFiles, string fqQcOpts, string extn) {

    string fastqcReport = sikDir+"/"+"fastqcReport"
    if((!fastqcReport.exists()) && (qc)) {
        fastqcReport.mkdir()
    }

    if(!cmdExe.hasKey("fastqcExe")) {
        error "Can't get fastqc executable, check your config file $configFile"
    }
    string fastqcExe = cmdExe{"fastqcExe"}

    if(!cmdExe.hasKey("fastqcMem")) {
        error "Can't get memory settings for FastQC, check your config file $configFile"
    }
    int fastqcMem = cmdExe{"fastqcMem"}.parseInt()

    if(!cmdExe.hasKey("fastqcCpu")) {
        error "Can't get cpu settings for FastQC, check your config file $configFile"
    }
    int fastqcCpu = cmdExe{"fastqcCpu"}.parseInt()

    if( (fqFiles.isEmpty()) && (qc) ) {
        error "No FASTQ files found, use -fqDir /path/to/fastq/dir."
    }

    string[] fqHtmls

    string preFix = "_fastqc.html"

    for(string fq : fqFiles) {
        string rootName = fq.baseName().removeExt(extn)
        string fqHtml = fastqcReport+"/"+rootName+preFix
        //NOTE this is a bit of hack where I assign 4 cpus for each task regardless of 
        // threads values passed in. Also note that I'm setting --threads to 8. I don't fully
        // understand this, but by doing so fastqc gets Xmx2000m i.e 2G of heap memory for every task
        // and this flag -XX:ParallelGCThreads=1, which I get doesn't use any of the extra cpus
        // this is to speed up processing. 
        // Basically on 16 core machines I'm allowing 4 parallel task to runs, each taking 2G of heap memory
        // and I think this is ok.. However in parallel there probably be also running another (multiple?) java process
        // that will also take up heap memory unknown amount. 
        dep(qc, fqHtml <- fq, cpus := fastqcCpu, taskName := "Running FASTQC") { 
            sys $fastqcExe $fqQcOpts --threads $fastqcCpu --outdir $fastqcReport $fq
        }
        fqHtmls.add(fqHtml)
    }
    goal fqHtmls
    return fqHtmls
}
//
string getMultiqc(string{} cmdExe, string sikDir, string multiqcFile, string[] allDeps, string multiqcOpts) {

    if(!cmdExe.hasKey("multiqcExe")) {
        error "Can't get multiqc executable, check your config file $configFile"
    }
    string multiqcExe = cmdExe{"multiqcExe"}

    allDeps += multiqcFile

    string outdep = sikDir+"/"+"multiqc_report.html"
    dep(multiqc, outdep <- allDeps, canFail := true, taskName := "Generating MultiQC report") {
        sys $multiqcExe --outdir $sikDir \
                        --file-list $multiqcFile \
                        $multiqcOpts \
                        -f
    }
    goal outdep
    return outdep
}

string[] getAddMetrics(string{} cmdExe, string{} bamsMap, string fastaRef, string fastaDictFile) {
    if(!cmdExe.hasKey("picardExe")) error "Can't get picard executable, check your config file $configFile"
    string picardExe = cmdExe{"picardExe"}

    string[] addMetrics

    for(string bamFile : bamsMap.values()) {

        string insertMetrics = bamFile.replace(".bam", "_insert_size.metrics")
        string hist = bamFile.replace(".bam", "_insert_size_hist.pdf")
        //
        string alignMetrics = bamFile.replace(".bam", "_align.metrics")
        //
        string gcMetrics = bamFile.replace(".bam", "_gc.metrics")
        string gcMetricsPdf = bamFile.replace(".bam", "_gc.pdf")
        string gcMetricsSummary = bamFile.replace(".bam", "_gc_summary.metrics")
        //
        string libComplex = bamFile.replace(".bam", "_lib_complex.metrics")

        addMetrics += insertMetrics
        addMetrics += hist
        addMetrics += alignMetrics
        addMetrics += gcMetrics
        addMetrics += gcMetricsPdf
        addMetrics += gcMetricsSummary
        addMetrics += libComplex

        task(qc && paired, [insertMetrics, hist] <- bamFile, cpus := 2, taskName := "gathering metrics "+bamFile, canFail := true) {
            sys $picardExe CollectInsertSizeMetrics VALIDATION_STRINGENCY=LENIENT \
                                                    INPUT=$bamFile \
                                                    OUTPUT=$insertMetrics \
                                                    HISTOGRAM_FILE=$hist \
                                                    M=0.5 
        }
        task(qc && !alignMetrics.exists(), alignMetrics <- [bamFile, fastaRef, fastaDictFile], cpus := 2, taskName := "gathering metrics "+bamFile, canFail := true) {
            sys $picardExe CollectAlignmentSummaryMetrics VALIDATION_STRINGENCY=LENIENT \
                                                          REFERENCE_SEQUENCE=$fastaRef \
                                                          INPUT=$bamFile \
                                                          OUTPUT=$alignMetrics
        }
        task(qc && !gcMetrics.exists(), [gcMetrics, gcMetricsPdf, gcMetricsSummary] <- [bamFile, fastaRef, fastaDictFile], cpus := 2, taskName := "gathering metrics "+bamFile, canFail := true) {
            sys $picardExe CollectGcBiasMetrics VALIDATION_STRINGENCY=LENIENT \
                                                INPUT=$bamFile \
                                                OUTPUT=$gcMetrics \
                                                CHART_OUTPUT=$gcMetricsPdf \
                                                SUMMARY_OUTPUT=$gcMetricsSummary \
                                                R=$fastaRef
        }
        task(qc && !libComplex.exists(), libComplex <- bamFile, cpus := 2, taskName := "gathering metrics "+bamFile, canFail := true) {
            sys $picardExe EstimateLibraryComplexity VALIDATION_STRINGENCY=LENIENT \
                                                     INPUT=$bamFile \
                                                     OUTPUT=$libComplex
        }
    }
    return addMetrics
}
