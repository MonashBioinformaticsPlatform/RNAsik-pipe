
include "sikConfigClass.bds"
include "sikSamtoolsUtils.bds"

class SikSamtoolsSortConfig extends SikResourcesConfig {

    string{} sortRes
    string[] sortCmdOpts

    string{} _sikSortConf // private to this class

    void SikSamtoolsSortConfig(string{} usrResConf, string{} usrToolsConf) {

        SikResourcesConfig(usrResConf)

        string{} usrSortOpts

        if(usrToolsConf.hasKey("samtoolsSort")) {
            usrSortOpts = parseUsrToolsOpts("samtoolsSort", usrToolsConf{"samtoolsSort"})
        }

        //TODO instead of creating dummy/empty files, I should just check here if the file exists
        string sikSortConfigFn = this._configOriginFn + "/sikCmdOptions/samtoolsSort.config"
        assert(sikSortConfigFn.exists())
        this._sikSortConf = config(sikSortConfigFn)

        string[] keys = this._sikSortConf.keys()
        keys += usrSortOpts.keys()
        keys = unique(keys)

        for(string k : keys) {
            if(usrSortOpts.hasKey(k)) {
                sortCmdOpts.add([k, usrSortOpts{k}].join(" "))
            } else {
                sortCmdOpts.add([k, this._sikSortConf{k}].join(" "))
            }
        }

        string[] sanityChk = ["samtoolsExe", \
                              "samtoolsSortMem", \
                              "samtoolsSortCpu"]

        for(string k : sanityChk) {
            if(!this._sikResConf.hasKey(k)) {
                error "\n\
                       \n\
                       SikErr: Key not found -> $k. Check you configuration files \
                       \n"
            }
        }

        sortRes{"idxCpu"} = this._sikResConf{"samtoolsSortCpu"}
        sortRes{"idxMem"} = this._sikResConf{"samtoolsSortMem"}
        sortRes{"samtoolsExe"} = this._sikResConf{"samtoolsExe"}
    }
}
