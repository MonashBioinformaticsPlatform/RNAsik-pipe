include "sikConfigClass.bds"
include "sikPicardUtils.bds"

class PicardMarkDupsConfig extends SikResourcesConfig {

    string{} markDupsRes
    string[] markDupsCmdOpts

    string{} _sikMarkDupsConf // private to this class

    void PicardMarkDupsConfig(string{} usrResConf, string{} usrToolsConf) {

        SikResourcesConfig(usrResConf)

        string{} usrMarkDupsOpts

        if(usrToolsConf.hasKey("picardMarkDups")) {
            usrMarkDupsOpts = parseUsrToolsOpts("picardMarkDups", usrToolsConf{"picardMarkDups"})
        }

        //TODO instead of creating dummy/empty files, I should just check here if the file exists
        string sikMarkDupsConfigFn = this._configOriginFn + "/sikCmdOptions/picardMarkDups.config"
        assert(sikMarkDupsConfigFn.exists())
        this._sikMarkDupsConf = config(sikMarkDupsConfigFn)

        string[] keys = this._sikMarkDupsConf.keys()
        keys += usrMarkDupsOpts.keys()
        keys = unique(keys)

        for(string k : keys) {
            if(usrMarkDupsOpts.hasKey(k)) {
                markDupsCmdOpts.add([k, usrMarkDupsOpts{k}].join(" "))
            } else {
                markDupsCmdOpts.add([k, this._sikMarkDupsConf{k}].join(" "))
            }
        }

        string[] sanityChk = ["picardExe", \
                              "picardMarkDupsMem", \
                              "picardMarkDupsCpu"]

        for(string k : sanityChk) {
            if(!this._sikResConf.hasKey(k)) {
                error "\n\
                       \n\
                       SikErr: Key not found -> $k. Check you configuration files \
                       \n"
            }
        }

        markDupsRes{"idxCpu"} = this._sikResConf{"picardMarkDupsCpu"}
        markDupsRes{"idxMem"} = this._sikResConf{"picardMarkDupsMem"}
        markDupsRes{"picardExe"} = this._sikResConf{"picardExe"}
        markDupsRes{"tmpDir"} = this._sikResConf{"tmpDir"}
    }
}
