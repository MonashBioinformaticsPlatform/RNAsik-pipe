#/usr/bin/env bds

#----------------------------------------------------------------------------------------------------
# RNAsik is fully sick way to get from FASTQ to list of counts, ready to load Degust file and more !
#----------------------------------------------------------------------------------------------------

#------------------------------
#  Serine #MonashFlavour
#------------------------------

#----------------------------------------------------------------------------------------------------
#
#    About RNAsik
#
#   * Takes a whole directory with FASTQ files
#   * Understands if FASTQ files are paired end or single end
#   * Understands if samples span different lanes. Merges all sample's lane at the mappinig step
#   * Gets count files, cleans them up and adds gene symbols and biotypes.
#   * Finds out library strand
#
#----------------------------------------------------------------------------------------------------
include "sikHeader.bds"
include "sikSanityCheck.bds"
include "sikExtraOptions.bds"
include "sikMakeIndices.bds"
include "sikLog.bds"
include "sikFqFiles.bds"
include "sikSTARaligner.bds"
include "sikCounts.bds"
include "sikPrePro.bds"
include "sikQCreports.bds"
include "sikExonicRate.bds"
// Check cmd args first up
string checkCmdInput = checkCmdArgs()
// set main directory
string sikDir
if(!outDir.isEmpty()) {
    if(!outDir.exists()) {
        outDir.mkdir()
        sikDir = outDir
    }
    else sikDir = outDir
}
else sikDir = "sikRun"
// make directories as required
if(!sikDir.exists()) sikDir.mkdir()
string logFile = sikDir+"/"+"logFile.txt"
string refFiles = sikDir+"/"+"refFiles"
if(!refFiles.exists()) refFiles.mkdir()
string bamFiles = sikDir+"/"+"bamFiles"
if( (!bamFiles.exists()) && (!fqDir.isEmpty()) ) bamFiles.mkdir()
string countFiles = sikDir+"/"+"countFiles"
if((!countFiles.exists()) && (count)) countFiles.mkdir()
string markedDupsBams = sikDir+"/"+"markedDupsBams"
if((!markedDupsBams.exists()) && (prePro)) markedDupsBams.mkdir()
//string qualiMapResults = sikDir+"/"+"qualiMapResults"
//if((!qualiMapResults.exists()) && (exonicRate)) qualiMapResults.mkdir()
string fastqcReport = sikDir+"/"+"fastqcReport"
if((!fastqcReport.exists()) && (fastqc)) fastqcReport.mkdir()
// Start loging
string date = sys date
logit(logFile, ">Starting new RNAsik run", date.trim())
#------------------------------
# Parse config file
#------------------------------
if(!configFile.isEmpty()) logit(logFile, "Config file parameters passed by user", configFile.read())
else {
    string getProgPath = programPath.pathName()
    configFile = getProgPath+"/../sik.config"
    logit(logFile, "Paths to the executables", configFile.read())
}
string{} cmdExe = config(configFile)
// local Fasta file
if( (!fastaRef.isEmpty()) && (!fastaRef.isFile()) ) error "Check your path to fasta file. It dosen't point to a file. $fastaRef"
string localFastaRef = makeLocalFastaRef(fastaRef, refFiles)
logit(logFile, "Referece FASTA file", localFastaRef)
// local GTF file
if( (!gtfFile.isEmpty()) && (!gtfFile.isFile()) ) error "Check your path to gtf file. It dosen't point to a file. $gtfFile"
string localGTF = makeLocalGTF(gtfFile, refFiles)
logit(logFile, "Reference GTF file", localGTF)
if(localGTF.extName() == "gff") setGFF = true
logit(logFile, "You are using GFF annotation file", setGFF)
// get .dict file 
string fastaDictFile = makeDictFile(cmdExe, localFastaRef)
logit(logFile, "Picard dictionary file", fastaDictFile)
// STAR index cmd options
string{} idxOpts
if(align == "starWithAnn") idxOpts = {"--sjdbGTFfile" => localGTF, "--sjdbOverhang" => 100 }
if(setGFF) idxOpts{"--sjdbGTFtagExonParentTranscript"} = "Parent" 
//
string allIdxOpts = getOpts(getOptsMap("starIndex", idxOpts, extraOpts))
logit(logFile, "STAR indexing cmd options", allIdxOpts)
// get genomeIdx directory
if( !align.isEmpty() && genomeIdx.isEmpty() ) {
    genomeIdx = makeSTARindex(cmdExe, refFiles, localFastaRef, localGTF, allIdxOpts, threads)
} 
logit(logFile, "STAR genomic index directory", genomeIdx)
// list of fastq files
string[] fqFiles
// grab all FASTQ files recursively
if(!fqDir.isEmpty()) fqFiles = getFqFiles(fqDir, extn)
string{} fqMap
if(!fqDir.isEmpty()) fqMap = getFqMap(fqFiles, fqRegex, samplesSheet, extn)
// for loggin purpose convert map to string and write it to a file
//string toString = fqMap
//logit(logFile, "Fastq files map, where key = rootName = bam file name and value actual fastq file with that rootName", toString)
logit(logFile, "Fastq files and sample names", parseFqMap(fqMap))
// STAR default options
string{} starOpts = { "--outSAMtype" => "BAM Unsorted", \
                      "--outSAMunmapped" => "Within"}
// use right file streaming cmd
if(extn.endsWith(".gz")) starOpts{"--readFilesCommand"} = "zcat"
else starOpts{"--readFilesCommand"} = "cat"
//
string allSTARopts = getOpts(getOptsMap("STAR", starOpts, extraOpts))
logit(logFile, "STAR alignment cmd options", allSTARopts)
//string  list of bam files from STAR run
string[] bamsList = mapSTAR(cmdExe, bamFiles, fqMap, allSTARopts, genomeIdx, threads, logFile)
logit(logFile, "BAM files", parseList(bamsList))
// featureCounts cmd options
string{} countsOpts = { "-a" => localGTF }
if(paired) countsOpts{"-p"} = ""
string allCountOpts = getOpts(getOptsMap("featureCounts", countsOpts, extraOpts))
logit(logFile, "featureCounts cmd options", allCountOpts)
//
if(!fqMap && ( (count) || (prePro) || (exonicRate) ) ) {
    warning "No paired information found. Use -paired to indicated paired end data"
    // get bam file from the bamFiles directory
    if(bamFiles.exists()) bamsList = bamFiles.dirPath("*.bam")
    else error "No bam files or bamFiles directory was found. You can pass bam files using -bamFiles /path/to/bamFiles/dir"
}
// where BAM had been sorted, reorted and duplicates marked
string[] markedBams = getMarkedBams(cmdExe, markedDupsBams, bamsList, localFastaRef, fastaDictFile, logFile)
logit(logFile, "Pre-processed BAM files", parseList(markedBams))
// get counts
string[] featureCounts = getReadsCount(cmdExe, countFiles, bamsList, allCountOpts, localGTF, threads)
logit(logFile, "Raw counts from featureCounts", parseList(featureCounts))
// get strand info
string strandInfo = getStrandInfo(cmdExe, featureCounts)
//NOTE I don't like strandInfo.read() operation becuase strandInfo file doesn't always exists
if(strandInfo.exists()) strandInfo = strandInfo.read()
else strandInfo = "Unknown"
logit(logFile, "The library strandness was detected as:", strandInfo)
logit(logFile, "The library appears to be paired end:", paired)
if(exonicRate) {
    //TODO maybe error is to strong. Have a think of a way to including warning instead. 
    if(!count && strand.isEmpty()) error "No strand information found. Use -count flag to get it. Alternatively use -strand flag with appropriate strand type."
    if(!count && !strand.isEmpty()) strandInfo = strand
}
string[] degust = getDegust(cmdExe, sikDir, featureCounts, bamsList.size(), localGTF, setGFF, threads)
logit(logFile, "Degust ready, protein_coding counts only", parseList(degust))
string{} exonicOpts = { "-gtf" => localGTF }
if(paired) {
    // flag for paired end data
    exonicOpts{"--paired"} = ""
    // bams need to be sorted by name, which is default output from STAR
    //TODO need to reinforce this! If user will output SortedByCoordinates from STAR 
    // then this will no longer be true
    exonicOpts{"--sorted"} = ""
}
string allExonicOpts = getOpts(getOptsMap("qualimap", exonicOpts, extraOpts))
logit(logFile, "QualiMap additional cmd options", allExonicOpts)
// get Int(ra|er)genic rates
string[] exonicRates = getExonicRates(cmdExe, sikDir, bamsList, allExonicOpts, strandInfo, logFile)
logit(logFile, "Exonic rates files", parseList(exonicRates))
// FastQC report
string{} fastqcOpts = { "--format" => "fastq", \
                        "--quiet" => ""}
//fastqcOpts{ "--casava" } = ""
string fqQcOpts = getOpts(getOptsMap("fastqc", fastqcOpts, extraOpts))
logit(logFile, "FastQC additional cmd options", fqQcOpts)
string[] fqQcReport = getFastqcReport(cmdExe, fastqcReport, fqFiles, fqQcOpts, extn, threads)
logit(logFile, "FastQC reports", parseList(fqQcReport))
// gather all files up for multiqc 
string[] multiqcDeps
if(!align.isEmpty()) multiqcDeps += bamsList
if(prePro) multiqcDeps += markedBams
if(count) multiqcDeps += degust
if(fastqc) multiqcDeps += fqQcReport
if(exonicRate) multiqcDeps += exonicRates
logit(logFile, "MultiQC dependency files", parseList(multiqcDeps))
string qcReport = getMultiqc(cmdExe, sikDir, multiqcDeps)
logit(logFile, "Multiqc will run on this directory", qcReport)
#----------------------------------------------------------------------------------------------------
# End
#----------------------------------------------------------------------------------------------------
