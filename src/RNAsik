#!/usr/bin/env bds

#----------------------------------------------------------------------------------------------------
# RNAsik-pipe is an easy way from FASTQ to differential gene expression #degust
#----------------------------------------------------------------------------------------------------

#------------------------------
#  Serine #MonashFlavour
#------------------------------

#----------------------------------------------------------------------------------------------------
#
#    About RNAsik-pipe
#
#   * Takes a whole directory with FASTQ files
#   * Understands if FASTQ files are paired end or single end
#   * Understands if samples span different lanes. Merges all sample's lane at the STAR runtime
#   * Gets count data, checks both reverse strand and no strands 
#   * Gets both RNA-SeQC report and Fastqc report
#
#----------------------------------------------------------------------------------------------------
include "sikHeader.bds"
include "sikSanityCheck.bds"
include "sikExtraOptions.bds"
include "sikMakeIndices.bds"
include "sikLog.bds"
include "sikFqFiles.bds"
include "sikSTARaligner.bds"
include "sikCounts.bds"
include "sikPrePro.bds"
include "sikQCreports.bds"
// Check cmd args first up
string checkCmdInput = checkCmdArgs()
#------------------------------
# Make directories
#------------------------------
string sikDir
if(!outDir.isEmpty()) {
    if(!outDir.exists()) {
        outDir.mkdir()
        sikDir = outDir
    }
    else sikDir = outDir
}
else sikDir = "sikRun"

if(!sikDir.exists()) sikDir.mkdir()
string logFile = sikDir+"/"+"logFile.txt"
string refFiles = sikDir+"/"+"refFiles"
if(!refFiles.exists()) refFiles.mkdir()
string bamFiles = sikDir+"/"+"bamFiles"
if( (!bamFiles.exists()) && (!align.isEmpty()) ) bamFiles.mkdir()
string countFiles = sikDir+"/"+"countFiles"
if((!countFiles.exists()) && (count)) countFiles.mkdir()
string markedDupsBams = sikDir+"/"+"markedDupsBams"
if((!markedDupsBams.exists()) && (prePro)) markedDupsBams.mkdir()
string fastqcReport = sikDir+"/"+"fastqcReport"
if((!fastqcReport.exists()) && (fastqc)) fastqcReport.mkdir()
// Start loging
string date = sys date
logit(logFile, ">Starting new RNAsik-pipe run", date.trim())
#------------------------------
# Parse config file
#------------------------------
if(!configFile.isEmpty()) logit(logFile, "Config file parameters passed by user", configFile.read())
else {
    string getProgPath = programPath.pathName()
    configFile = getProgPath+"/../sik.config"
    logit(logFile, "Default config file parameters", configFile.read())
}
string{} cmdExe = config(configFile)
#------------------------------
# Global Variables
#------------------------------
// list of fastq files
string[] fqFiles
// local Fasta file
if( (!fastaRef.isEmpty()) && (!fastaRef.isFile()) ) error "Check your path to fasta file. It dosen't point to a file. $fastaRef"
string localFastaRef = makeLocalFastaRef(fastaRef, refFiles)
// local GTF file
if( (!gtfFile.isEmpty()) && (!gtfFile.isFile()) ) error "Check your path to gtf file. It dosen't point to a file. $gtfFile"
string localGTF = makeLocalGTF(gtfFile, refFiles)
if(localGTF.extName() == "gff") setGFF = true
// Make indices if required 
logit(logFile, "FASTA file in use", localFastaRef)
logit(logFile, "Annotation file in use", localGTF)
// 
string fastaDictFile = makeDictFile(cmdExe, localFastaRef)
logit(logFile, "FASTA dict file for picard", fastaDictFile)
// STAR index cmd options
string{} idxOpts
string allIdxOpts = getOpts(getOptsMap("starIndex", idxOpts, extraOpts))
logit(logFile, "STAR indexing cmd options", allIdxOpts)
// get genomeIdx directory
if( !fastaRef.isEmpty() && genomeIdx.isEmpty() && (!align.isEmpty() || align == "starIdx") ) {
    genomeIdx = makeSTARindex(cmdExe, refFiles, localFastaRef, localGTF, allIdxOpts, threads)
} 
logit(logFile, "STAR genomic index", genomeIdx)
// grab all FASTQ files
fqFiles = getFqFiles(fqDir, extn)
string{} fqMap = getFqMap(fqFiles, fqRegex, samplesSheet, extn)
// for loggin purpose convert map to string and write it to a file
string toString = fqMap
logit(logFile, "Fastq files map, where key = rootName = bam file name and value actual fastq file with that rootName", toString)
// STAR default options
string{} starOpts = { "--outSAMtype" => "BAM Unsorted", \
                      "--outSAMunmapped" => "Within"}

if(extn.endsWith(".gz")) starOpts{"--readFilesCommand"} = "zcat"
else starOpts{"--readFilesCommand"} = "cat"

string allSTARopts = getOpts(getOptsMap("STAR", starOpts, extraOpts))
logit(logFile, "STAR alignment cmd options", allSTARopts)
//string  list of bam files from STAR run
string[] bamsList = mapSTAR(cmdExe, bamFiles, fqMap, allSTARopts, genomeIdx, threads, logFile)
// featureCounts cmd options
string{} countsOpts = { "-a" => localGTF }
if(paired) countsOpts{"-p"} = ""
string allCountOpts = getOpts(getOptsMap("featureCounts", countsOpts, extraOpts))
//
if(!fqMap && ( (count) || (prePro) ) ) {
    warning "No information about paired end. Use -paired to indicated paired end data"
    // get bam file from the bamFiles directory
    if(bamFiles.exists()) bamsList = bamFiles.dirPath("*.bam")
    else error "No bam files or bamFiles directory was found. You can pass bam files using -bamFiles /path/to/bamFiles/dir"
}
// where BAM had been sorted, reorted and duplicates marked
string[] markedBams = getMarkedBams(cmdExe, markedDupsBams, bamsList, localFastaRef, fastaDictFile, logFile)
// get counts
string[] featureCounts = getReadsCount(cmdExe, countFiles, bamsList, allCountOpts, localGTF, threads)
string strandInfo = getStrandInfo(cmdExe, featureCounts)
string[] degust = getDegust(cmdExe, sikDir, featureCounts, bamsList.size(), localGTF, setGFF, threads)
// FastQC report
string{} fastqcOpts = { "--format" => "fastq", \
                        "--quiet" => ""}
//fastqcOpts{ "--casava" } = ""
string fqQcOpts = getOpts(getOptsMap("fastqc", fastqcOpts, extraOpts))
string[] fqQcReport = getFastqcReport(cmdExe, fastqcReport, fqFiles, fqQcOpts, extn, threads)
// gather all files up for multiqc 
string[] multiqcDeps = bamFiles + markedBams + degust + fqQcReport
//
string qcReport = getMultiqc(cmdExe, sikDir, multiqcDeps)
logit(logFile, "Multiqc will run on this directory", qcReport)
#----------------------------------------------------------------------------------------------------
# End
#----------------------------------------------------------------------------------------------------
