#------------------------------
# Sanity check
#------------------------------

include "sikHeader.bds"
include "sikMakeIndices.bds"

if(align == "star") {
    // if regex string was given, be sure to set the right regex
    if(!fqRegex.isEmpty()){
        if(fqRegex == "A") fqRegex = "_L[0-9]{3}_R[0-9]_[0-9]{3}.fastq.gz$"
        if(fqRegex == "B") fqRegex = "_L[0-9]{3}_R[0-9].fastq.gz$"
        if(fqRegex == "C") fqRegex = "_R[0-9]_[0-9]{3}.fastq.gz$"
        if(fqRegex == "D") fqRegex = "_R[0-9].fastq.gz$"
    }
}
if(!strand.isEmpty()) {
    if(strand == "0") strand = "non-strand-specific"
    if(strand == "1") strand = "strand-specific-forward"
    if(strand == "2") strand = "strand-specific-reverse"
    //TODO need to check users input, only allo 0,1 or 2 
}

if(!samplesSheet.isEmpty()) if(!samplesSheet.isFile()) error "-samplesSheet doesn't appear to be pointing at the valid file, double check your path $samplesSheet"
if(!fqDir.isEmpty()) {
    if(!fqDir.isDir()) error "-fqDir doesn't appear to be pointing at the valid directory, double check your path $fqDir"
    if(align.isEmpty() && !fastqc) warning "-align and -fastqc are empty, not sure why -fqDir $fqDir was specified"
} 
if(count) if(gtfFile.isEmpty()) error "Usage: -gtfFile /path/to/GTFfile"
if(prePro) if(fastaRef.isEmpty()) error "Usage: -fastaRef /path/to/referenceFastaFile"
if(mapRate) if(gtfFile.isEmpty()) error "Usage: -gtfFile /path/to/GTFfile"
if(!configFile.isEmpty()) if(!configFile.isFile()) error "-configFile doesn't appear to be pointing at the valid file, double check your path $configFile"

if( args.isEmpty() ) {
    printHelp()
    exit(1)
}
// this functions checks if all arguments on the cmd are right
string checkCmdArgs() {
    // list of allowed command line options
    string[] sikArgs = ["-align", \
                        "-fqDir", \
                        "-fqRegex", \
                        "-genomeIdx", \
                        "-count", \
                        "-gtfFile", \
                        "-prePro", \
                        "-fastaRef", \
                        "-fastqc", \
                        "-mapRate", \
                        "-strand", \
                        "-threads", \
                        "-extn", \
                        "-samplesSheet", \
                        "-extraOpts", \
                        "-paired", \
                        "-bamFiles", \
                        "-proBams", \
                        "-configFile", \
                        "-multiqc", \
                        "-outDir", \
                        "-version" \
                       ]
    // list of options specified by user
    string[] getCmdArgs = args 
    // check for duplicates
    string[] suspects
    for(string cmdItem : args) {
        if(cmdItem.startsWith("-")) {
            if(!sikArgs.has(cmdItem)) error "This flag $cmdItem isn't a valid options, check your spelling"
            if(suspects.has(cmdItem)) error "This flag $cmdItem is duplicated in your command, remove duplicate"
	    suspects.add(cmdItem)
        }
    }
    return "thumbs up"
}
