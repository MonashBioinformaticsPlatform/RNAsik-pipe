
string{} getSortedBams(string{} cmdExe, string sikDir, string{} bamsMap) {

    string bamFiles = sikDir+"/"+"bamFiles"
    if( !bamFiles.exists() && (mdups || !align.isEmpty()) ) {
        bamFiles.mkdir()
    }

    string{} resource = getResource(cmdExe, "samtools", "Sort")
    string samtoolsExe = resource{"toolsExe"}
    int sortMem = resource{"toolsMem"}.parseInt()
    int sortCpu = resource{"toolsCpu"}.parseInt()

    //NOTE we want samtools sort to recieve sortMem memory from sik.cofig
    //However this is per thread and therefore samtools sort task should be set
    // at sortMem*sortCpu
    int sortBdsMem = sortMem*sortCpu
    // need to scale down by 80 % because sam takes a bit more RAM then actually given
    real scalFact = 0.85
    // covert to kilobytes, divide by number of threads (since samtools allocates this much memory per-thread)
    string sortSamMem = round(scalFact * ((sortMem/1000) / sortCpu))+"K"

    string[] sortedBams
    string{} sortedBamsMap

    for(string bamKey : bamsMap.keys()) {

        string bamFile = bamsMap{bamKey}
        //TODO not sure if I'm passing _T correctly, need to test before including into main line
        //string tmpPrefix = bamFiles+"/"+bamKey+"_"+randInt(10000000)
        //-T $tmpPrefix \
        string sortedBam = bamFiles+"/"+bamKey+"_sorted.bam"
        string sortedLog = bamFiles+"/."+bamKey+"_sorted.siklog"

        dep(mdups || !align.isEmpty(), sortedBam <- bamFile, cpus := sortCpu, mem := sortBdsMem, taskName := "sorting with samtools "+bamFile) {
            sys $samtoolsExe sort --threads $sortCpu \
                                  -m $sortSamMem \
                                  -o $sortedBam \
                                  $bamFile > $sortedLog 2>&1
            sys $samtoolsExe index $sortedBam
            sys rm $bamFile
        }
        sortedBams.add(sortedBam)
        if(sortedBamsMap.hasKey(bamKey)) {
            error "This should happened, please report this issue, sikPrePro.bds function"
        }
        sortedBamsMap{bamKey} = sortedBam
       
    }
    // for more infor refer to sikSTARaligner.bds line 129 comment
    //if(!mdups) {
    //    goal sortedBams
    //}
    return sortedBamsMap
}

string{} getDeDupedBams(string{} cmdExe, string{} bamsMap) {

    string{} resource = getResource(cmdExe, "picard", "MarkDups")
    string picardExe = resource{"toolsExe"}
    int picardMem = resource{"toolsMem"}.parseInt()
    int picardCpu = resource{"toolsCpu"}.parseInt()

    string[] markedBams
    string{} markedBamsMap

    for(string bamKey : bamsMap.keys()) {

        string bamFile = bamsMap{bamKey}
        string bamFileIdx = bamFile+".bai"
        string mdupsName = bamFile.swapExt(".bam", "_mdups.bam")
        string metricsName = bamFile.swapExt(".bam", "_mdups.metrics")
        string hackMdups = mdupsName.baseName()
        string mdupsLog = mdupsName.replace(hackMdups, "."+hackMdups).removeExt()+".siklog"

        dep(mdups && !umi, [mdupsName, metricsName] <- bamFile, cpus := picardCpu, mem := picardMem, taskName := "markiing duplicates with picard MarkDuplicates "+bamFile) {
            sys $picardExe MarkDuplicates TMP_DIR=tmp/ \
                                          VALIDATION_STRINGENCY=LENIENT \
                                          CREATE_INDEX=true \
                                          INPUT=$bamFile \
                                          OUTPUT=$mdupsName \
                                          METRICS_FILE=$metricsName > $mdupsLog 2>&1
            sys rm $bamFile $bamFileIdx
        }
        markedBams.add(mdupsName)
        if(markedBamsMap.hasKey(bamKey)) {
            error "This should happened, please report this issue, sikPrePro.bds function"
        }
        markedBamsMap{bamKey} = mdupsName
    }
    //goal markedBams
    return markedBamsMap
}

string{} getDeDupedBamsUmi(string{} cmdExe, string{} bamsMap) {

    string{} resource = getResource(cmdExe, "je", "")
    string jeExe = resource{"toolsExe"}
    int jeMem = resource{"toolsMem"}.parseInt()
    int jeCpu = resource{"toolsCpu"}.parseInt()

    string{} resource2 = getResource(cmdExe, "picard", "MarkDups")
    string picardExe = resource2{"toolsExe"}

    string[] markedBams
    string{} markedBamsMap

    for(string bamKey : bamsMap.keys()) {

        string bamFile = bamsMap{bamKey}
        string bamFileIdx = bamFile+".bai"
        string mdupsName = bamFile.swapExt(".bam", "_umi_mdups.bam")
        string metricsName = bamFile.swapExt(".bam", "_umi_mdups.metrics")
        string hackMdups = mdupsName.baseName()
        string mdupsLog = mdupsName.replace(hackMdups, "."+hackMdups).removeExt()+".siklog"

        dep(mdups && umi, [mdupsName, metricsName] <- bamFile, cpus := jeCpu, mem := jeMem, taskName := "marking duplicates with je "+bamFile) {
            sys $jeExe markdupes \
                       INPUT=$bamFile \
                       OUTPUT=$mdupsName \
                       MISMATCHES=0 \
                       METRICS_FILE=$metricsName \
                       REMOVE_DUPLICATES=true \
                       ASSUME_SORTED=true > $mdupsLog 2>&1

            sys $picardExe BuildBamIndex INPUT=$mdupsName

            sys rm $bamFile $bamFileIdx
        }
        markedBams.add(mdupsName)
        if(markedBamsMap.hasKey(bamKey)) {
            error "This should happened, please report this issue, sikProcBam.bds function"
        }
        markedBamsMap{bamKey} = mdupsName
       
    }
    //goal markedBams
    return markedBamsMap
}

string{} getMarkedBams(string{} cmdExe, string{} bamsMap) {
    if(umi) {
        return getDeDupedBamsUmi(cmdExe, bamsMap)
    }
    else {
        return getDeDupedBams(cmdExe, bamsMap)
    }
}

