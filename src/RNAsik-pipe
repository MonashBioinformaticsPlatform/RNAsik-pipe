#!/usr/bin/env bds

#----------------------------------------------------------------------------------------------------
# RNAsik-pipe is an easy way from FASTQ to differential gene expression #degust
#----------------------------------------------------------------------------------------------------

#------------------------------
#  Serine #MonashFlavour
#------------------------------

#----------------------------------------------------------------------------------------------------
#
#    About RNAsik-pipe
#
#   * Takes a whole directory with FASTQ files
#   * Understands if FASTQ files are paired end or single end
#   * Understands if samples span different lanes. Merges all sample's lane at the STAR runtime
#   * Gets count data, checks both reverse strand and no strands 
#   * Prepares BAM files for RNAseQC report
#   * Gets both RNA-SeQC report and Fastqc report
#
#----------------------------------------------------------------------------------------------------

include "sikHeader.bds"
include "sikSanityCheck.bds"
include "sikMakeIndices.bds"
include "sikFunctions.bds" 
include "sikPrePro.bds"
include "sikCounts.bds"
include "sikQCreports.bds"
#------------------------------
# Print version
#------------------------------
if(args[0] == "-version") print("RNAsik-pipe version 1.3-alpha\n")

#------------------------------
# Make indices if required 
#------------------------------
if(makeIndices){
    localFastaRef = makeLocalFastaRef(fastaRef)
    string sjdbArguments
    if(gtfFile) {
        int readSize = 101
        int overHang = readSize - 1
    
        sjdbArguments = "--sjdbGTFfile $gtfFile --sjdbOverhang $overHang"
    }
    else sjdbArguments = ""

    string[] localIndexList = makeSTARindex(localFastaRef, sjdbArguments)
    localSAindexFile = localIndexList[0]
    localGenomeIndex = localIndexList[1]

    string[] fastaIndexFiles = makeOtherIndices(localFastaRef)
    fastaDictFile = fastaIndexFiles.pop()
    fastaFaiFile = fastaIndexFiles.pop()
}

if(makeIndex){
    localFastaRef = makeLocalFastaRef(fastaRef)
    string[] fastaIndexFiles = makeOtherIndices(localFastaRef)
    fastaDictFile = fastaIndexFiles.pop()
    fastaFaiFile = fastaIndexFiles.pop()
}
#------------------------------
# fastQC report
#------------------------------
if(fastqc) {
    if(!fqDir.isEmpty()) {
        string fqcReport = getFastqcReport(fqDir)
    }
    if(!fqDirs.isEmpty()) {
        string[] dirs = getDirs(fqDirs)
        int numberOfDirs = dirs.size()

        for(int i=0; i < numberOfDirs; i++){
            string dir = dirs[i]
            string fqcReport = getFastqcReport(dir)
        }
    }
}
#------------------------------
# BAM generation step
#------------------------------
if(star){
    if(!fqDir.isEmpty()) {
        string{} uniqueNames = getUniqueNames(fqDir)

        for(string fqread : uniqueNames.keys()) {
            # get value for each key and break values into list
            string mapValue = uniqueNames{fqread} 
            string[] getList = mapValue.split(",")
            string[] orderedList = getOrderedList(getList)
            string boolItem = orderedList.pop()
            checkPair = boolItem.parseBool()
            # STAR aligning
            string[] reads = getSTARreads(orderedList, checkPair)
            string bamFile = mapSTAR(fqread, localGenomeIndex, localSAindexFile, reads, orderedList) 
            bamsList.add(bamFile)
        }
    }

    if(!fqDirs.isEmpty()) {
        string[] dirs = getDirs(fqDirs)
        int numberOfDirs = dirs.size()
    
        for(int i=0; i < numberOfDirs; i++){
        
            string dir = dirs[i]
            string{} uniqueNames = getUniqueNames(dir)
    
            for(string fqread : uniqueNames.keys()) {
                # get value for each key and break values into list
                string mapValue = uniqueNames{fqread} 
                string[] getList = mapValue.split(",")
                string[] orderedList = getOrderedList(getList)
                string boolItem = orderedList.pop()
                checkPair = boolItem.parseBool()
                # STAR aligning
                string[] reads = getSTARreads(orderedList, checkPair)
                string bamFile = mapSTAR(fqread, localGenomeIndex, localSAindexFile, reads, orderedList) 
                bamsList.add(bamFile)
            }
        }
    }
}


if(count) { 
   string[] featureCounts = getReadsCount(bamsList, gtfFile, checkPair)
   string featureNo = featureCounts[0]
   string featureReverse = featureCounts[1]

   string featureNoCounts = "featureNoCounts.txt"
   string featureReverseCounts = "featureReverseCounts.txt"

   string tempNo = "tempNo.txt"
   string tempReverse = "tempReverse.txt"

   string featureNoCountsWithNames = "featureNoCounts-withNames.txt"
   string featureReverseCountsWithNames = "featureReverseCounts-withNames.txt"

   string geneIdsFile = getGeneIds()

   int numberOfBamFiles = bamsList.size()+7
   int[] theRange = range(7, numberOfBamFiles)
   string numberOfFields = theRange.join(",")

task([featureNoCounts, featureReverseCounts] <- [featureCounts, geneIdsFile], taskName := "Making Degust-ready counts file") {
       sys grep -v "#" $featureNo | cut -f1,$numberOfFields | perl -pe 's{\S+/([^_]+)[^/]+(\t|\n)}{$1$2}g if $. == 1' > $featureNoCounts
       sys sed -i '0,/Geneid/s/Geneid/Gene.ID/' $featureNoCounts
       sys /bin/bash -c "join -j 1 <(sort -k1,1 $geneIdsFile) <(sort -k1,1 $featureNoCounts) -t $'\t' > $tempNo"
       sys grep "Biotype" $tempNo > $featureNoCountsWithNames
       sys grep -v "Biotype" $tempNo >> $featureNoCountsWithNames
       sys rm $tempNo

       sys grep -v "#" $featureReverse | cut -f1,$numberOfFields | perl -pe 's{\S+/([^_]+)[^/]+(\t|\n)}{$1$2}g if $. == 1' > $featureReverseCounts
       sys sed -i '0,/Geneid/s/Geneid/Gene.ID/' $featureReverseCounts
       sys /bin/bash -c "join -j 1 <(sort -k1,1 $geneIdsFile) <(sort -k1,1 $featureReverseCounts) -t $'\t' > $tempReverse"
       sys grep "Biotype" $tempReverse > $featureReverseCountsWithNames
       sys grep -v "Biotype" $tempReverse >> $featureReverseCountsWithNames
       sys rm $tempReverse
   }
   goal [featureNoCounts, featureReverseCounts]
}
#------------------------------
# BAM processing
#------------------------------
for(string bamFile : bamsList) {
    if(prePro) {
        string testCheck = getPrePro(bamFile, localFastaRef)
        markedBams.add(testCheck)
    }
}
#----------------
# RNAseQC report
#----------------
if(RNAseQC){
    string sampleIdsFile = "RNAseQC-sampleIds.txt"
    string RNAseQCsampleIds = getSampleIds(markedBams)
    sampleIdsFile.write(RNAseQCsampleIds)
    string check = getRNAseQC(sampleIdsFile, markedBams, checkPair, localFastaRef)
}
#----------------------------------------------------------------------------------------------------
# End
#----------------------------------------------------------------------------------------------------
